import org.gradle.internal.jvm.Jvm

plugins {
    id 'java-library'
    id 'checkstyle'
    id "net.ltgt.errorprone" version "0.0.16"
    id 'com.bmuschko.nexus' version '2.3.1'
    id 'io.codearte.nexus-staging' version '0.8.0'
}

group = 'com.imsweb'
version = file('VERSION').text.trim()
description = 'Framework that allows defining file formats (layouts) and use them to read and write data files.'

println "Starting build using ${Jvm.current()}"

repositories {
    mavenCentral()
}

dependencies {
    api 'com.imsweb:naaccr-xml:4.10'
    api 'au.com.bytecode:opencsv:2.4'
    api 'com.thoughtworks.xstream:xstream:1.4.10'

    testImplementation 'junit:junit:4.12'

    errorprone 'com.google.errorprone:error_prone_core:2.3.1'
}

// enforce UTF-8, display the compilation warnings
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}

// the Javadoc was made way too strict in Java 8 and it's not worth the time fixing everything!
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

// customize the manifest
jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': version,
                'Implementation-Vendor': 'Information Management Services Inc.',
                'Created-By': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')',
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version')
        )
    }
}

// checkstyle plugin settings
checkstyle {
    configFile = file('config/checkstyle/checkstyle.xml')
}

// needed to deploy to Maven Central
modifyPom {
    project {
        name 'Layout Framework'
        description 'Framework that allows defining file formats (layouts) and use them to read and write data files.'
        url 'https://github.com/imsweb/layout'
        inceptionYear '2015'

        scm {
            url 'https://github.com/imsweb/layout'
            connection 'scm:https://github.com/imsweb/layout.git'
            developerConnection 'scm:git@github.com:imsweb/layout.git'
        }

        licenses {
            license {
                name 'A modified BSD License (BSD)'
                url 'https://github.com/imsweb/layout/blob/master/LICENSE'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'depryf'
                name 'Fabian Depry'
                email 'depryf@imsweb.com'
            }
        }
    }
}

// don't try to release a snapshot to a non-snapshot repository, that won't work anyway
if (version.endsWith('-SNAPSHOT'))
    gradle.startParameter.excludedTaskNames += 'closeAndReleaseRepository'

// Gradle wrapper, this allows to build the project without having to install Gradle!
wrapper {
    gradleVersion = '4.9'
}


