plugins {
    id 'java-library'
    id 'checkstyle'
    id 'com.github.spotbugs' version '4.5.1'
    id 'com.bmuschko.nexus' version '2.3.1'
    id 'io.codearte.nexus-staging' version '0.21.2'
}

group = 'com.imsweb'
version = file('VERSION').text.trim()
description = 'Framework that allows defining file formats (layouts) and use them to read and write data files.'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

println "Starting build using JDK ${Runtime.version().feature()}"

repositories {
    mavenCentral()

    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    api 'com.imsweb:naaccr-xml:7.10'

    implementation 'commons-io:commons-io:2.7'
    implementation 'com.opencsv:opencsv:5.4'
    implementation 'com.thoughtworks.xstream:xstream:1.4.16'

    testImplementation 'junit:junit:4.13'
    testImplementation 'com.imsweb:seerutils-gui:1.15'
    testImplementation 'com.imsweb:seerutils:5.6'
    testImplementation 'org.xhtmlrenderer:flying-saucer-core:9.1.19'
}

// enforce UTF-8, display the compilation warnings
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation' << '-Xlint:rawtypes' << '-Werror'
}

// the Javadoc was made way too strict in Java 8 and it's not worth the time fixing everything!
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

// customize the manifest
jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'Information Management Services Inc.',
                'Created-By': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')',
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Automatic-Module-Name': 'com.imsweb.layout'
        )
    }
}

// checkstyle plugin settings
checkstyle {
    toolVersion = '8.14'
    ignoreFailures = true
    configFile = project(':').file('config/checkstyle/checkstyle.xml')
    configProperties = ['suppressionFile': project(':').file('config/checkstyle/suppressions.xml')]
}

// spotbugs plugin settings
spotbugs {
    toolVersion = '3.1.9'
    ignoreFailures = true
    excludeFilter = project(':').file("config/spotbugs/spotbugs-exclude.xml")
}

// configure nexus staging plugin
nexusStaging {
    numberOfRetries = 50
    delayBetweenRetriesInMillis = 5000
}

// needed to deploy to Maven Central
modifyPom {
    project {
        name 'Layout Framework'
        description 'Framework that allows defining file formats (layouts) and use them to read and write data files.'
        url 'https://github.com/imsweb/layout'
        inceptionYear '2015'

        scm {
            url 'https://github.com/imsweb/layout'
            connection 'scm:https://github.com/imsweb/layout.git'
            developerConnection 'scm:git@github.com:imsweb/layout.git'
        }

        licenses {
            license {
                name 'A modified BSD License (BSD)'
                url 'https://github.com/imsweb/layout/blob/master/LICENSE'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'depryf'
                name 'Fabian Depry'
                email 'depryf@imsweb.com'
            }
        }
    }
}

// don't try to release a snapshot to a non-snapshot repository, that won't work anyway
if (version.endsWith('-SNAPSHOT'))
    gradle.startParameter.excludedTaskNames.add('closeAndReleaseRepository')

// Gradle wrapper, this allows to build the project without having to install Gradle!
wrapper {
    gradleVersion = '6.7'
    distributionType = Wrapper.DistributionType.ALL
}


